/*
 * generated by Xtext
 */
package io.pelle.mango.dsl.generator.server.service

import com.google.inject.Inject
import io.pelle.mango.dsl.generator.server.ServerNameUtils
import io.pelle.mango.dsl.mango.Model
import io.pelle.mango.dsl.mango.Service
import java.util.Map
import org.gwtwidgets.server.spring.GWTHandler
import org.springframework.beans.factory.annotation.Autowired
import org.springframework.context.annotation.Bean
import org.springframework.context.annotation.Configuration
import java.util.HashMap

class GWTSpringServices {

	@Inject
	extension ServerNameUtils
	
	def gwtRemoteServicesApplicationContext(Model model) '''
		package «model.gwtRemoteServicesApplicationContextPackageName»;

		@«Configuration.name»
		class «model.gwtRemoteServicesApplicationContextName» {

			«FOR service: model.eAllContents.toIterable.filter(Service)»
			@«Autowired.name»
			private «service.serviceInterfaceFullQualifiedName» «service.serviceSpringName»;
			
			«ENDFOR»
			
			@«Bean.name»
			@«Autowired.name»
			public «GWTHandler.name» «model.modelName.toFirstLower»(RPCServiceExporterFactory serviceExporterFactory) {
				«GWTHandler.name» result = new «GWTHandler.name»();
				result.setServiceExporterFactory(serviceExporterFactory);

				«Map.name» mappings = new «HashMap.name»();
				
				«FOR service: model.eAllContents.toIterable.filter(Service)»
				mappings.put("/rpc/«service.serviceSpringName»", «service.serviceSpringName»);
				«ENDFOR»
				
				result.setMappings(mappings);
				
				return result; 
			}
		}
	'''

	def restRemoteServicesApplicationContext(Model model) '''
	package «model.restRemoteServicesApplicationContextPackageName»;

	@«Configuration.name»
	class «model.restRemoteServicesApplicationContextName» {
	
		«FOR service: model.eAllContents.toIterable.filter(Service)»
		@«Bean.name»
		public «service.restControllerFullQualifiedName» «service.restControllerBeanName»() {
			return new «service.restControllerFullQualifiedName»();
		}
		«ENDFOR»
	
	}
	'''
	
}
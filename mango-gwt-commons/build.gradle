buildscript {
    repositories {
        maven {
            url 'https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/'
        }
        
        mavenCentral()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.4'
    }
}

apply plugin: 'gwt'

jar {
	from sourceSets.main.allSource
	from sourceSets.main.output.classesDir
}

configurations {
	gwt
	gwtTest
}

dependencies {
	gwt group: 'com.google.gwt', name: 'gwt-user', version: '2.7.0'
	compile files('build/gwt-user-repackaged.jar')
	compile group: 'com.google.guava', name: 'guava-gwt', version: versions.guava
	compile 'org.gwtbootstrap3:gwtbootstrap3:0.9.1'
}

configurations {
	gwt
}

gwt {
    gwtVersion = versions.gwt
    modules 'io.pelle.mango.gwt.commons.MangoGwtCommonsDemo'
	logLevel = 'ERROR'
}


eclipse {
	classpath {
		minusConfigurations += [ configurations.gwt ]
	}
}

task expandGwtUserJar(type: Copy) {

	from {
	
		def jarFile = project.configurations.gwt.find { File file -> file.absolutePath =~ /.*gwt-user.*/ }
		
		if (!jarFile) {
		    throw new Exception("gwt-user jar file not found!")
		}
		
		zipTree(jarFile)
	}
	
	into file("$buildDir/gwt-user")
}

task assembleGwtUserJar(type: Zip, dependsOn: expandGwtUserJar) {
	destinationDir file("$buildDir")
	archiveName 'gwt-user-repackaged.jar'
	from file("$buildDir/gwt-user")
	exclude 'javax/**'
}

eclipseClasspath.dependsOn("assembleGwtUserJar")
compileJava.dependsOn("assembleGwtUserJar")

compileGwt.onlyIf { !project.hasProperty('skipCompileGwt') }

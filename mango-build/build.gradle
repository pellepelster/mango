apply from: '../mango-build/mangocommon.gradle'

buildscript {

	repositories {
	
		ivy {
			name "localDevelopmentRepository"
			url System.properties['user.home'] + "/.gradle/mango_development"
			layout "maven"
		}
		
    	jcenter()
    	
    	mavenCentral()

	}
	
	dependencies {
		classpath group: 'org.xtext', name: 'xtext-gradle-plugin', version: '0.3.27'
		classpath group: 'io.pelle.mango', name: 'mango-gradle-plugin', version: '+'
		classpath group: 'de.undercouch', name: 'gradle-download-task', version: '2.0.0'
  }
}

ext {

	versions = [
		spring: "4.2.4.RELEASE",
		spring_security: "4.0.2.RELEASE",
		gwt: "2.7.0",
		guava: "17.0",
		xtext: "2.8.4",
		metrics: "3.1.2",
		xtend_gradle: "0.4.12",
		xtext_gradle: "0.3.27",
		hsqldb: "2.3.3"
	]

  eclipse_url = 'http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/modeling/tmf/xtext/downloads/distros/eclipse-dsl-mars-1-linux-gtk-x86_64.tar.gz'

  mango_gradle_dir = new File(project.gradle.gradleUserHomeDir, 'mango')

}

import de.undercouch.gradle.tasks.download.Download

task downloadEclipse(type: Download) {
    
    def destFile = new File(mango_gradle_dir, 'eclipse.tar.gz')

    println "downloading eclipse to '" + destFile + "'"

    src eclipse_url
    dest destFile
    outputs.file destFile
}

task downloadAndExtractEclipse(dependsOn: downloadEclipse, type: Copy) {
    from tarTree(downloadEclipse.dest)
    into mango_gradle_dir
}

build.dependsOn downloadAndExtractEclipse

allprojects {

	apply from: '../mango-build/mangojavacommon.gradle'

	apply plugin: 'eclipse'

	eclipse.classpath.file {
	    whenMerged { classpath ->
	        classpath.entries.findAll { entry -> entry.kind == 'var' && entry.path.contains('com.google.gwt') }*.exported = false
	    }
	}
	
	configurations {
		provided
	}
	
	sourceSets {
		main {
			java { srcDirs = ["src"] }
			resources { srcDirs = ["src"] }
		}
		test {
			java { srcDirs = ["test"] }
			resources { srcDirs = ["test"] }
		}
	}
	
	sourceSets.main.compileClasspath += [ configurations.provided ]
	
	javadoc {
	    	exclude "**/**"
	}

	eclipse {
		pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
		
	  classpath {
		  plusConfigurations += [ configurations.provided ]
	  }
	}

	dependencies {
		compile group: 'log4j', name: 'log4j', version: '1.2.17'
		testCompile group: 'junit', name: 'junit', version: '4.11'
	}

}
buildscript {
    repositories {
        maven {
            url 'https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/'
        }
        
        mavenCentral()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.4'
    }
}

apply plugin: 'gwt'

configurations {
	gwt {
		extendsFrom compile
	}
	gwtTest {
		extendsFrom gwt
	}
}

dependencies {
	compile project(path: ':mango-demo-model', configuration: 'generatedClientGWT')
	compile project(path: ':mango-client-web-test')
	compile project(path: ':mango-client-gwt')
	gwtTest group: 'com.google.gwt', name: 'gwt-dev', version: versions.gwt
}

gwt {
    gwtVersion = versions.gwt
    modules 'io.pelle.mango.demo.DemoClient'
	logLevel = 'ERROR'
	maxHeapSize = "2048M";

	src += files(project(':mango-client-base').sourceSets.main.allJava.srcDirs) + 
			files(project(':mango-client-core').sourceSets.main.allJava.srcDirs) + 
			files(project(':mango-client-web').sourceSets.main.allJava.srcDirs) +
			files(project(':mango-client-gwt').sourceSets.main.allJava.srcDirs)
}

ext.gwtTestLibDir = project.file(new File('lib-gwt-test'))

task copyGwtTestLibs(type: Copy) {
	gwtTestLibDir.mkdirs()
	into gwtTestLibDir
	from configurations.gwtTest
}

test.dependsOn(project(':mango-demo-server').tasks.test)

compileGwt.onlyIf { !project.hasProperty('skipCompileGwt') }
